{ config, lib, pkgs, inputs, ... }:

let
  cfg = config.programs.kanshi;
in {
  options.programs.kanshi = {
    enable = lib.mkEnableOption "kanshi output configuration daemon";

    package = lib.mkOption {
      type = lib.types.package;
      default = pkgs.kanshi;
      defaultText = "pkgs.kanshi";
      description = "The kanshi package to use.";
    };

    extraConfig = lib.mkOption {
      type = lib.types.lines;
      default = "";
      description = ''
        Additional configuration to append to the kanshi config file.
      '';
    };
  };

  config = lib.mkIf cfg.enable {
    environment.systemPackages = [ cfg.package ];

    environment.etc."xdg/kanshi/config".text = ''
      # Kanshi configuration generated by NixOS
      profile {
        output DP-3 mode 2560x1440@164.956
        output eDP-2 disable
      }
      profile {
        output * enable
      }
      ${cfg.extraConfig}
    '';

    systemd.user.services.kanshi = {
      description = "Kanshi output configuration daemon";
      after = [ "graphical-session.target" ];
      wantedBy = [ "graphical-session.target" ];
      serviceConfig = {
        ExecStart = "${cfg.package}/bin/kanshi -c /etc/xdg/kanshi/config";
        Restart = "on-failure";
      };
    };
  };
}
