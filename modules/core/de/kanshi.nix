{ config, lib, pkgs, inputs, ... }:

let
  cfg = config.programs.kanshi;
in {
  options.programs.kanshi = {
    enable = lib.mkEnableOption "kanshi output configuration daemon";

    wallpaper = lib.mkOption {
      type = lib.types.nullOr lib.types.path;
      default = null;
      description = ''
        Absolute path to the wallpaper image to set via awww.
        If null, no wallpaper command is added to the kanshi config.
      '';
    };

    wallpaperBlur = lib.mkOption {
      type = lib.types.nullOr lib.types.path;
      default = null;
      description = ''
        Absolute path to the blurred wallpaper image to set via awww.
        If null, no wallpaper command is added to the kanshi config.
      '';
    };

    package = lib.mkOption {
      type = lib.types.package;
      default = pkgs.kanshi;
      defaultText = "pkgs.kanshi";
      description = "The kanshi package to use.";
    };

    extraConfig = lib.mkOption {
      type = lib.types.lines;
      default = "";
      description = ''
        Additional configuration to append to the kanshi config file.
      '';
    };
  };

  config = lib.mkIf cfg.enable {
    environment.systemPackages = [ cfg.package ];

    environment.etc."xdg/kanshi/config".text = ''
      # Kanshi configuration generated by NixOS
      profile {
        output DP-3 mode 2560x1440@164.956
        output eDP-2 disable
        ${lib.optionalString (cfg.wallpaper != null) ''exec /run/current-system/sw/bin/awww img ${cfg.wallpaper}''}
        ${lib.optionalString (cfg.wallpaper != null) ''exec /run/current-system/sw/bin/awww img --namespace blur ${cfg.wallpaperBlur}''}
      }
      profile {
        output * enable
        ${lib.optionalString (cfg.wallpaper != null) ''exec /run/current-system/sw/bin/awww img ${cfg.wallpaper}''}
        ${lib.optionalString (cfg.wallpaper != null) ''exec /run/current-system/sw/bin/awww img --namespace blur ${cfg.wallpaperBlur}''}
      }
      ${cfg.extraConfig}
    '';

    systemd.user.services.kanshi = {
      description = "Kanshi output configuration daemon";
      after = [ "graphical-session.target" ];
      wantedBy = [ "graphical-session.target" ];
      serviceConfig = {
        ExecStart = "${cfg.package}/bin/kanshi -c /etc/xdg/kanshi/config";
        Restart = "on-failure";
      };
    };
  };
}
